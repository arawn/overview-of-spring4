import com.sun.tools.javac.resources.compiler

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.0'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'tomcat'
apply from: 'idea.gradle'

ext {
    versions = [
            spring:'4.0.1.RELEASE',
            jackson: '2.3.1',
            aspectj: '1.7.4',
            slf4j: '1.7.5',
            logback: '1.0.13',
            groovy: '2.2.1',
            springSpock: '0.7-groovy-2.0',
            tomcat: '7.0.47'
    ]
}

configurations {
    compile.exclude group: 'commons-logging', module: 'commons-logging'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven { url 'http://repo.spring.io/libs-release' }
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    mavenCentral()
}

dependencies {
    compile "org.springframework:spring-context:$versions.spring",
            "org.springframework:spring-aop:$versions.spring",
            "org.springframework:spring-jdbc:$versions.spring",
            "org.springframework:spring-webmvc:$versions.spring",
            "org.springframework:spring-websocket:$versions.spring",
            "org.springframework:spring-messaging:$versions.spring"

    compile "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson",
            "com.fasterxml.jackson.core:jackson-databind:$versions.jackson"

    compile "org.aspectj:aspectjrt:$versions.aspectj",
            "org.aspectj:aspectjweaver:$versions.aspectj",
            "org.aspectj:aspectjtools:$versions.aspectj"

    compile "ch.qos.logback:logback-classic:$versions.logback",
            "org.slf4j:jcl-over-slf4j:$versions.slf4j"

    compile "org.codehaus.groovy:groovy-all:$versions.groovy"

    compile "javax.servlet:jstl:1.2"
    providedCompile "org.apache.tomcat:tomcat-servlet-api:$versions.tomcat",
                    "org.apache.tomcat:tomcat-jsp-api:$versions.tomcat",
                    "org.apache.tomcat:tomcat-websocket-api:$versions.tomcat"


    testCompile "org.springframework:spring-test:$versions.spring",
                "org.spockframework:spock-spring:$versions.springSpock",
                "cglib:cglib-nodep:2.2.2",
                "com.h2database:h2:1.3.172"

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$versions.tomcat",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:$versions.tomcat"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:$versions.tomcat") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

tomcatRunWar {
    httpPort = 8090
    stopPort = 8091
    contextPath = ''
    reloadable = true
}